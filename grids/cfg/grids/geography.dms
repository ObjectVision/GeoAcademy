container Geography: Using = "Units"
{
	unit<fpoint>  rdc : Format   = "EPSG:28992", DialogData = "ngr_layer";

	#include<ngr_layer.dms>
	
	parameter<m> x_min := 61182[m];
	parameter<m> x_max := 62920[m];
	parameter<m> y_min := 365813[m];
	parameter<m> y_max := 367135[m];

	container grid_05m      := define_grid(0.5[m], 0.5[m], .);
	container grid_10m      := define_grid(10[m] , 10[m] , .);

	unit<spoint> kernel_buffer := range(spoint, point(-2s, -2s), point(3s,3s))
	{
		attribute<uint32>  dist     := dist2(point(0s, 0s, .), uint32);
		attribute<float32> distMatr := dist <= 5 ? 1f : 0f;
	}
	
	template define_grid
	{
		// begin case parameters
		parameter<m> gridsizeX;
		parameter<m> gridsizeY;
		container src;
		// end case parameters

		parameter<int16> nr_cols := int16((src/x_max - src/x_min) / gridsizeX);
		parameter<int16> nr_rows := int16((src/y_max - src/y_min) / gridsizeY);

		unit<spoint> domain :=
			range(
				 gridset(
					 rdc
					,point(float32(gridsizeX), float32(-gridsizeY), rdc)
					,point(src/x_min, src/y_max, rdc)
					,spoint
				)
				,point(0s, 0s)
				,point(nr_cols,nr_rows)
			)
		,	DialogData = "rdc";
	}
	
}