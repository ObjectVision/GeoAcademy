container game_of_life
{
	container simulation_parameters
	{
		parameter<UInt32>  number_of_steps := 1000; 
		parameter<Float32> fraction_alive_cells := 0.25f;
		parameter<UInt32>  square_grid_size := 2000;
		
		unit<UInt32> steps := range(UInt32, 0, number_of_steps)
		{
			attribute<.> id        			:= id(.);
			attribute<.> id_prev            := id(.)-1;
			attribute<String> name 			:= "step_" + String(id);
			attribute<String> expr   		:= "simulation_step(simulation/" + "step_" + String(id_prev) + "/next_generation)";
			attribute<String> expr_final	:= id == 0 ? "simulation_step(generation_domain/initial_state)" : expr; 
		}
	}
	
	unit<upoint> generation_domain := range(upoint, point(0u, 0u), point(simulation_parameters/square_grid_size, simulation_parameters/square_grid_size))
	{
		attribute<float32> random_field := rnd_uniform(0, ., range(float32, 0f, 1f));
		attribute<float32> initial_state   := Float32(random_field <= simulation_parameters/fraction_alive_cells);
	}
	
	unit<upoint> kernel_domain := range(spoint, point(0u, 0u), point(3u, 3u))
    {
	  attribute<.> id := id(.);
	  attribute<Float32> kernel := id == point(1u,1u) ? 0f : 1f;
    }
	
	Template simulation_step
	{
		attribute<Float32> current_generation (generation_domain);
		attribute<UInt4> nr_alive_cells (generation_domain) := UInt4(potential(current_generation, kernel_domain/kernel));
		attribute<bool> underpopulated (generation_domain)  := nr_alive_cells < 2u4;
		attribute<bool> overpopulated (generation_domain)   := nr_alive_cells > 3u4;
		attribute<bool> reproducing (generation_domain)     := nr_alive_cells == 3u4;
		
		attribute<Float32> next_generation (generation_domain)     := Float32(!(underpopulated || overpopulated) && reproducing);
		
	}
	
	Container simulation := for_each_ind('ne', simulation_parameters/steps/name, simulation_parameters/steps/expr_final);
	
	unit<upoint> export_simulation := generation_domain,
	  StorageName = "%LocalDataProjDir%/sim_result.tif" 
	, StorageType = "gdalwrite.grid"
	{
	   attribute<float32> GridData := = 'simulation/step_' + String(max(simulation_parameters/steps/id)) + '/next_generation';
	}
}
}